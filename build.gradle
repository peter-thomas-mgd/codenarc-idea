// codenarc-disable
import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType

plugins {
    id 'org.jetbrains.intellij.platform' version '2.10.4'
    id 'java'
    id 'idea'
    id 'groovy'
    id 'application'
}

if (!project.hasProperty('publishPluginChannels')) ext.publishPluginChannels = ''
if (!project.hasProperty('publishPluginToken')) ext.publishPluginToken = '**UNDEFINED**'
if (!project.hasProperty('ideVersion')) ext.ideVersion = '2025.2'

project.group = 'org.codenarc.idea'
project.version = ext.pluginVersion

wrapper {
    gradleVersion = '9.2.0'
    distributionType = Wrapper.DistributionType.ALL
    // Checksums are found here: https://gradle.org/release-checksums/
    distributionSha256Sum = '47a5bfed9ef814f90f8debcbbb315e8e7c654109acd224595ea39fca95c5d4da'
}

idea {
    project {
        jdkName = '17'
    }
}

sourceSets.create 'generator', {
    compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
    runtimeClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath
    groovy {
        srcDirs 'src/generator'
    }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    generatorImplementation.extendsFrom compile, implementation, runtimeOnly
}

configurations.configureEach {
    resolutionStrategy {
        // Force a single Groovy version across all modules
        force "org.apache.groovy:groovy:${groovyVersion}"
        force "org.apache.groovy:groovy-xml:${groovyVersion}"
    }
}

tasks.configureEach { task ->
    if (task.name.contains('prepareTest') && task.name.contains('Sandbox')) {
        task.doLast {
            def testPluginLib = file("build/idea-sandbox/IU-2025.2.4/plugins-test/${project.name}/lib")
            if (testPluginLib.exists()) {
                def removed = []
                testPluginLib.listFiles()?.findAll {
                    it.name.startsWith('spock-')
                }?.each {
                    removed << it.name
                    it.delete()
                }
                if (removed) {
                    logger.lifecycle("Removed from test sandbox: ${removed}")
                }
            }
        }
    }
}

dependencies {
    intellijPlatform {
        intellijIdea '2025.2.4'
        bundledPlugin 'com.intellij.java'
        bundledPlugin 'org.intellij.groovy'
        testFramework TestFrameworkType.Platform.INSTANCE
        testFramework TestFrameworkType.Plugin.Java.INSTANCE
    }
    compileOnly 'org.apache.groovy:groovy', {
        version {
            strictly groovyVersion
        }
        because 'there are version conflicts when building'
    }
    implementation "org.codenarc:CodeNarc:$codenarcVersion", {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'org.apache.groovy'
    }

    implementation 'io.sentry:sentry:5.1.0'

    runtimeOnly 'org.gmetrics:GMetrics-Groovy4:2.1.0', {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'org.appache.groovy'
    }

    compile 'org.apache.commons:commons-lang3:3.18.0'

    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.10.0'
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation 'com.agorapulse.testing:fixt:0.2.4'
    testImplementation 'org.apache.groovy:groovy', {
        version {
            strictly groovyVersion
        }
        because 'there are version conflicts when building'
    }

    generatorImplementation 'org.apache.groovy:groovy', {
        version {
            strictly groovyVersion
        }
        because 'there are version conflicts when building'
    }
}

logger.lifecycle("Using IDE version $ideVersion")

/*
intellij {
    pluginName = 'CodeNarc'

    downloadSources = !providers.environmentVariable('CI').forUseAtConfigurationTime().present
    plugins = ['Groovy', 'gradle', 'java']
    version = idePlatformVersion
    instrumentCode = false
}
 */

publishPlugin {
    token = publishPluginToken
    if (publishPluginChannels) {
        channels = Arrays.asList(publishPluginChannels.split(','))
    }
}

runIde {
    jvmArgs "-Xmx2g"
}

intellijPlatform.pluginVerification {
    ides {
        create(IntelliJPlatformType.IntellijIdea, ideVersion)
    }
}

patchPluginXml {
    version = project.version
    sinceBuild = '252.23892'
    untilBuild = '252.*'
    changeNotes = file("$rootDir/changelog.html").text
    pluginDescription = file("$rootDir/description.html").text
}

/** Regenerates IDEA inspections from CodeNarc */
application {
    mainClass = 'org.codenarc.idea.gen.RuleInspectionsGenerator'
}

run {
    // generate classes
    classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath +
        sourceSets.generator.compileClasspath + sourceSets.generator.runtimeClasspath
    args project.rootDir.canonicalPath
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}